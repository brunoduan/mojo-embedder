# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/features.gni")
import("//build/config/jumbo.gni")

# See //samples/BUILD.gn for how this works.
group("slaverer") {
  if (is_component_build) {
    public_deps = [
      "//samples",
    ]
  } else {
    public_deps = [
      ":slaverer_sources",
    ]
  }
}

if (is_component_build) {
  link_target_type = "jumbo_source_set"
} else {
  link_target_type = "jumbo_static_library"
}
target(link_target_type, "slaverer_sources") {
  # External code should depend on via ":slaverer" above.
  visibility = [ "//samples/*" ]

  sources = [
    "slaver_thread.cc",
    "slaver_thread.h",
    "slaver_thread_observer.cc",
    "slaver_thread_observer.h",
    "samples_slaverer_client.cc",
    "samples_slaverer_client.h",
    "seccomp_sandbox_status_android.h",
  ]

  configs += [ "//samples:samples_implementation" ]

  deps = [
    "//samples/public/child:child_sources",
    "//samples/public/common:common_sources",
    "//samples/slaverer",
  ]

  allow_circular_includes_from = [
    # This target is a pair with samples/slaverer. They always go together and
    # include headers from each other.
    "//samples/slaverer",
  ]

  if (is_android) {
    # For seccomp_sandbox_status_android.h.
    deps += [ "//sandbox" ]
  }
}
