# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# See //samples/BUILD.gn for how this works.
group("utility") {
  if (is_component_build) {
    public_deps = [
      "//samples",
    ]
  } else {
    public_deps = [
      ":utility_sources",
    ]
  }
}

source_set("utility_sources") {
  # External code should depend on via ":utility" above.
  visibility = [ "//samples/*" ]

  sources = [
    "samples_utility_client.cc",
    "samples_utility_client.h",
    "utility_thread.cc",
    "utility_thread.h",
  ]

  configs += [ "//samples:samples_implementation" ]

  deps = [
    "//base",
    "//samples:export",
    "//samples/public/child:child_sources",
    "//samples/public/common:common_sources",
    "//samples/utility",
    "//ipc",
  ]

  allow_circular_includes_from = [
    # This target is a pair with samples/browser. They always go together and
    # include headers from each other.
    "//samples/utility",
  ]
}
