
import("//mojo/public/tools/bindings/mojom.gni")
import("//base/android/jni_generator/jni_exception_list.gni")
import("//build/config/android/config.gni")
import("//build/config/android/rules.gni")

samples_shell_manifest = "$target_gen_dir/samples_shell_manifest/AndroidManifest.xml"

generate_jni("samples_shell_jni_headers") {
  jni_package = "samples/shell"
  sources = [
    "java/src/com/xpeng/samples/shell/ShellLauncher.java",
  ]
}

generate_jni_registration("samples_shell_jni_registration") {
  testonly = true
  target = ":samples_shell_apk"
  output = "$root_gen_dir/samples/shell/android/${target_name}.h"
  exception_files = jni_exception_files
}


shared_library("libsamples_shell") {
  testonly = true
  deps = [
    ":samples_shell_jni_registration",
    ":samples_shell_jni_headers",
    "//samples/shell:samples_shell_lib",
  ]
  sources = [
    "shell_library_loader.cc",
  ]
}


android_resources("samples_shell_java_resources") {
  testonly = true
  resource_dirs = [ "java/res" ]
  custom_package = "com.xpeng.samples_shell"
}


jinja_template("samples_shell_manifest") {
  testonly = true
  input = "shell_apk/AndroidManifest.xml.jinja2"
  output = samples_shell_manifest
}


android_resources("samples_shell_apk_resources") {
  testonly = true
  resource_dirs = [ "shell_apk/res" ]
  deps = [
    ":samples_shell_java_resources",
  ]
  android_manifest = samples_shell_manifest
  android_manifest_dep = ":samples_shell_manifest"
}


android_library("samples_shell_apk_java") {
  testonly = true
  deps = [ 
    ":samples_shell_apk_resources",
    ":samples_shell_manifest",
    ":samples_shell_java_resources",
    "//samples/public/android:samples_java",
  ]
 
  android_manifest_for_lint = samples_shell_manifest
 
  java_files = [ 
    "shell_apk/src/com/xpeng/samples_shell_apk/SamplesShellActivity.java",
    "shell_apk/src/com/xpeng/samples_shell_apk/SamplesShellApplication.java",
    "java/src/com/xpeng/samples/shell/ShellLauncher.java",
  ]
}


android_assets("samples_shell_assets") {
  disable_compression = true
  deps = [
    "//third_party/icu:icu_assets",
  ]
}                 
                  
android_apk("samples_shell_apk") {
  testonly = true
  enable_multidex = true
  deps = [        
    ":samples_shell_apk_java",
    ":samples_shell_assets",
    "//base:base_java",
    "//mojo/public/java:base_java",
  ]               
  apk_name = "MySamplesShell"
  android_manifest = samples_shell_manifest
  android_manifest_dep = ":samples_shell_manifest"
  shared_libraries = [ ":libsamples_shell" ]
}
